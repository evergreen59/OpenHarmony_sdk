# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/lite/config/component/lite_component.gni")
import("//build/ohos.gni")

## Build Contacts_DataAbility.hap
ohos_hap("Contacts_DataAbility") {
  hap_profile = "entry/src/main/module.json"
  deps = [
    ":Contacts_DataAbility_js_assets",
    ":Contacts_DataAbility_resources",
  ]
  shared_libraries = [ ":contactsdataability" ]
  certificate_profile = "./signature/contactsdata.p7b"
  hap_name = "Contacts_DataAbility"
  part_name = "contactsdata_hap"
  subsystem_name = "applications"
  module_install_dir = "app/com.ohos.contactsdataability"
}

ohos_app_scope("Contacts_DataAbility_app_profile") {
  app_profile = "AppScope/app.json"
  sources = [ "AppScope/resources" ]
}

ohos_js_assets("Contacts_DataAbility_js_assets") {
  ets2abc = true
  source_dir = "entry/src/main/ets"
}

ohos_resources("Contacts_DataAbility_resources") {
  sources = [ "entry/src/main/resources" ]
  deps = [ ":Contacts_DataAbility_app_profile" ]
  hap_profile = "entry/src/main/module.json"
}

## Build contactsdataability.so {{{
config("contactsdataability_public_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "//third_party/node/src",
    "ability/common/include",
    "ability/common/utils/include/",
    "dataBusiness/voicemail/include",
    "dataBusiness/calllog/include",
    "dataBusiness/contacts/include",
    "ability/account/include",
    "dataBusiness/quicksearch/include",
    "ability/sinicization/include",
    "ability/account/include",
    "ability/merge/include",
    "ability/datadisasterrecovery/include",
  ]
  cflags = []
}

ohos_shared_library("contactsdataability") {
  sources = [
    "ability/account/src/account_data_collection.cpp",
    "ability/account/src/account_manager.cpp",
    "ability/account/src/account_sync.cpp",
    "ability/common/utils/src/contacts_common_event.cpp",
    "ability/common/utils/src/contacts_json_utils.cpp",
    "ability/common/utils/src/contacts_path.cpp",
    "ability/common/utils/src/contacts_string_utils.cpp",
    "ability/common/utils/src/file_utils.cpp",
    "ability/common/utils/src/merge_utils.cpp",
    "ability/common/utils/src/predicates_convert.cpp",
    "ability/common/utils/src/sql_analyzer.cpp",
    "ability/common/utils/src/uri_utils.cpp",
    "ability/datadisasterrecovery/src/database_disaster_recovery.cpp",
    "ability/merge/src/candidate.cpp",
    "ability/merge/src/candidate_status.cpp",
    "ability/merge/src/match_candidate.cpp",
    "ability/merge/src/merger_contacts.cpp",
    "ability/sinicization/src/character_transliterate.cpp",
    "ability/sinicization/src/construction_name.cpp",
    "dataBusiness/calllog/src/calllog_ability.cpp",
    "dataBusiness/calllog/src/calllog_database.cpp",
    "dataBusiness/contacts/src/contacts.cpp",
    "dataBusiness/contacts/src/contacts_account.cpp",
    "dataBusiness/contacts/src/contacts_data_ability.cpp",
    "dataBusiness/contacts/src/contacts_database.cpp",
    "dataBusiness/contacts/src/contacts_datashare_stub_impl.cpp",
    "dataBusiness/contacts/src/contacts_type.cpp",
    "dataBusiness/contacts/src/contacts_update_helper.cpp",
    "dataBusiness/contacts/src/profile_database.cpp",
    "dataBusiness/contacts/src/raw_contacts.cpp",
    "dataBusiness/quicksearch/src/contacts_search.cpp",
    "dataBusiness/voicemail/src/voicemail_ability.cpp",
    "dataBusiness/voicemail/src/voicemail_database.cpp",
  ]
  include_dirs = [
    "//commonlibrary/c_utils/base/include",
    "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    "//third_party/jsoncpp",
    "//third_party/jsoncpp/include/json",
    "//base/account/os_account/interfaces/innerkits/ohosaccount/native/include/",
    "//base/account/os_account/interfaces/kits/napi/distributedaccount/include/",
    "//base/account/os_account/interfaces/innerkits/include",
    "//foundation/ability/ability_runtime/interfaces/inner_api/ability_manager/include",
    "//foundation/ability/ability_runtime/interfaces/kits/native/ability/native",
  ]

  deps = [ "//third_party/jsoncpp:jsoncpp" ]

  public_configs = [ ":contactsdataability_public_config" ]
  external_deps = [
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:abilitykit_native",
    "ability_runtime:app_context",
    "ability_runtime:dataobs_manager",
    "c_utils:utilsbase",
    "common_event_service:cesfwk_innerkits",
    "data_share:datashare_common",
    "data_share:datashare_provider",
    "napi:ace_napi",
    "relational_store:native_dataability",
    "relational_store:native_rdb",
    "relational_store:rdb_data_share_adapter",
  ]

  defines = [
    "CONTACTSDATA_LOG_TAG = \"ContactsData\"",
    "LOG_DOMAIN = 0xD001F09",
  ]

  if (is_standard_system) {
    external_deps += [ "hiviewdfx_hilog_native:libhilog" ]
  } else {
    external_deps += [ "hilog:libhilog" ]
  }

  public_deps = []
  part_name = "contactsdata_hap"
  subsystem_name = "applications"
}
## Build contactsdataability.so }}}
