# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/account/os_account/os_account.gni")
import("//build/ohos.gni")

group("common_target") {
  deps = [ ":libaccount_common" ]
}

config("account_common_config") {
  include_dirs = [
    "file_operator/include",
    "log/include",
    "perf_stat/include",
    "//commonlibrary/c_utils/base/include",
    "//third_party/json/include",
    "${os_account_dfx_path}/hisysevent_adapter",
  ]
}

config("account_common_all_dependent_configs") {
  include_dirs = [
    "database/include",
    "account_error/include",
  ]
}

ohos_shared_library("libaccount_common") {
  sources = [
    "${os_account_dfx_path}/hisysevent_adapter/hisysevent_adapter.cpp",
    "account_error/src/account_error_no.cpp",
    "file_operator/src/account_file_operator.cpp",
    "log/src/account_log_wrapper.cpp",
    "perf_stat/src/perf_stat.cpp",
  ]

  cflags = [ "-pipe" ]
  cflags_cc = [
    "-Wdate-time",
    "-Wformat=2",
    "-Wfloat-equal",
    "-Wshadow",
  ]

  configs = [ "${account_coverage_config_path}:coverage_flags" ]

  defines = [
    "ACCOUNT_LOG_TAG = \"AccountCommonFwk\"",
    "LOG_DOMAIN = 0xD001B00",
  ]

  public_configs = [
    ":account_common_config",
    "//commonlibrary/c_utils/base:utils_config",
  ]

  all_dependent_configs = [ ":account_common_all_dependent_configs" ]

  public_deps = [ "//commonlibrary/c_utils/base:utils" ]

  external_deps = [ "hiviewdfx_hilog_native:libhilog" ]

  if (build_selinux) {
    external_deps += [ "selinux:librestorecon" ]
    cflags += [ "-DWITH_SELINUX" ]
  }

  part_name = "os_account"
}
