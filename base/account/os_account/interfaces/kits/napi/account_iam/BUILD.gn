# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/account/os_account/os_account.gni")
import("//build/ohos.gni")

config("account_iam_kit_config") {
  include_dirs = [
    "${account_iam_kits_path}/include",
    "//foundation/ace/napi/interfaces/kits",
    "//third_party/libuv/include",
    "//third_party/node/src",
    "${os_account_path}/interfaces/kits/napi/common/include",
    "//base/useriam/user_auth_framework/interfaces/inner_api",
  ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }

  cflags += [ "-pipe" ]
  cflags_cc = [
    "-Wdate-time",
    "-Wformat=2",
    "-Wfloat-equal",
    "-Wshadow",
  ]

  if (has_user_auth_part) {
    cflags_cc += [ "-DHAS_USER_AUTH_PART" ]
  }

  if (has_pin_auth_part) {
    cflags_cc += [ "-DHAS_PIN_AUTH_PART" ]
  }
}

ohos_shared_library("account_iam_napi") {
  include_dirs = []
  configs = [
    ":account_iam_kit_config",
    "${account_coverage_config_path}:coverage_flags",
  ]
  public_configs = [ ":account_iam_kit_config" ]
  sources = [
    "${account_iam_kits_path}/src/napi_account_iam_common.cpp",
    "${account_iam_kits_path}/src/napi_account_iam_constant.cpp",
    "${account_iam_kits_path}/src/napi_account_iam_identity_manager.cpp",
    "${account_iam_kits_path}/src/napi_account_iam_inputer_manager.cpp",
    "${account_iam_kits_path}/src/napi_account_iam_module.cpp",
    "${account_iam_kits_path}/src/napi_account_iam_pin_auth.cpp",
    "${account_iam_kits_path}/src/napi_account_iam_user_auth.cpp",
    "${os_account_path}/frameworks/common/account_error/src/account_error_no.cpp",
    "${os_account_path}/interfaces/kits/napi/common/src/napi_account_common.cpp",
    "${os_account_path}/interfaces/kits/napi/common/src/napi_account_error.cpp",
  ]

  defines = [
    "ACCOUNT_LOG_TAG = \"AccountIAM\"",
    "LOG_DOMAIN = 0xD001B00",
  ]

  deps = [
    "${account_iam_framework_path}:account_iam_innerkits",
    "//foundation/arkui/napi/:ace_napi",
  ]

  if (has_user_auth_part) {
    deps += [
      "//base/useriam/user_auth_framework/frameworks/native/client:userauth_client",
      "//base/useriam/user_auth_framework/frameworks/native/ipc:userauth_client_ipc",
    ]
  }

  external_deps = [
    "c_utils:utils",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
  ]

  subsystem_name = "account"
  part_name = "os_account"
}
