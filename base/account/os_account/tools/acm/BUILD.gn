# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/account/os_account/os_account.gni")
import("//build/ohos.gni")

config("tools_acm_config") {
  include_dirs = [
    "${ability_runtime_path}/services/common/include",
    "include",
  ]

  defines = [
    "ACCOUNT_LOG_TAG = \"AccountTool\"",
    "LOG_DOMAIN = 0xD001B00",
  ]
}

ohos_source_set("tools_acm_source_set") {
  sources = [
    "${ability_runtime_path}/tools/aa/src/shell_command.cpp",
    "src/account_command.cpp",
    "src/main.cpp",
  ]

  public_configs = [
    "${ability_runtime_path}/tools/aa:ability_command_config",
    ":tools_acm_config",
  ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  if (os_account_multiple_active_accounts) {
    cflags += [ "-DENABLE_MULTIPLE_ACTIVE_ACCOUNTS" ]
  }
  cflags += [ "-pipe" ]
  cflags_cc = [
    "-Wdate-time",
    "-Wformat=2",
    "-Wfloat-equal",
    "-Wshadow",
  ]

  deps = [ "${os_account_core_path}:os_account_core" ]

  external_deps = [
    "c_utils:utils",
    "hiviewdfx_hilog_native:libhilog",
  ]
  part_name = "os_account"
}

ohos_executable("acm") {
  deps = [ ":tools_acm_source_set" ]

  configs = [ "${account_coverage_config_path}:coverage_flags" ]

  install_enable = true

  subsystem_name = "account"
  part_name = "os_account"
}

group("tools_acm") {
  deps = [ ":acm" ]
}
