# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/hiviewdfx/faultloggerd/faultloggerd.gni")

if (defined(ohos_lite)) {
  executable("dumpcatcher") {
    visibility = [ "*:*" ]
    include_dirs = [
      ".",
      "$faultloggerd_path/common",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/utils",
      "//commonlibrary/c_utils/base/include",
      "$faultloggerd_path/tools/process_dump/",
      "$faultloggerd_path/interfaces/innerkits/dump_catcher/include",
      "$hilog_lite_include_path",
    ]
    sources = [
      "dump_catcher.cpp",
      "main.cpp",
    ]
    deps = [
      "$faultloggerd_path/common/dfxlog:static_common_log",
      "$faultloggerd_path/interfaces/innerkits/dump_catcher:lib_dfx_dump_catcher",
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//third_party/bounds_checking_function:libsec_shared",
    ]
  }
} else {
  config("dumpcatcher_config") {
    visibility = [ ":*" ]

    include_dirs = [
      ".",
      "//base/hiviewdfx/faultloggerd/common",
      "$faultloggerd_path/tools/process_dump/",
      "//base/hiviewdfx/faultloggerd/common/dfxlog",
      "//commonlibrary/c_utils/base/include",
      "$faultloggerd_path/interfaces/innerkits/dump_catcher/include",
    ]
  }

  ohos_executable("dumpcatcher") {
    install_enable = true
    configs = [ ":dumpcatcher_config" ]
    sources = [
      "dump_catcher.cpp",
      "main.cpp",
    ]

    cflags_cc = [ "-DDEBUG_CRASH_LOCAL_HANDLER" ]

    deps = [
      "$faultloggerd_path/common/dfxlog:static_common_log",
      "$faultloggerd_path/common/util:dfx_cutil",
      "$faultloggerd_path/interfaces/innerkits/dump_catcher:lib_dfx_dump_catcher",
      "$faultloggerd_path/interfaces/innerkits/signal_handler:dfx_local_handler",
    ]

    external_deps = [
      "c_utils:utils",
      "hilog_native:libhilog",
    ]

    install_images = [
      "system",
      "updater",
    ]

    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }
}
