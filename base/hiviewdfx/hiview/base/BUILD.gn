# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//base/hiviewdfx/hiview/hiview.gni")
import("//build/ohos.gni")

config("hiview_base_config") {
  visibility = [ "*:*" ]

  include_dirs = [
    "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    "//third_party/bounds_checking_function/include",
    "//commonlibrary/c_utils/base/include",
    "include",
    "utility/include",
    "event_pool",
    "event_store/include",
    "$hiview_root/include",
    "$hiview_adapter/utility/include",
    "logstore/include",
    "$hiview_adapter/dbstore/include",
  ]

  cflags_cc = [ "-D__HIVIEW_OHOS__" ]
  if (is_standard_system) {
    cflags_cc += [ "-D__OHOS_STANDARD_SYSTEM__" ]
  }
}

ohos_source_set("logger") {
  public_configs = [ ":hiview_base_config" ]
  sources = [
    "default_logger.cpp",
    "hi_logger.cpp",
    "logger.cpp",
  ]
}

ohos_source_set("hiview_base") {
  part_name = "hiview"
  public_configs = [ ":hiview_base_config" ]

  sources = [
    "audit.cpp",
    "audit_log_parser.cpp",
    "event.cpp",
    "event_dispatch_queue.cpp",
    "event_dispatcher.cpp",
    "event_loop.cpp",
    "event_source.cpp",
    "hiview_global.cpp",
    "pipeline.cpp",
    "plugin.cpp",
    "plugin_factory.cpp",
    "plugin_proxy.cpp",
    "sys_event.cpp",
  ]

  deps = [
    ":logger",
    "event_pool:eventpool",
    "event_report:hiview_event_report",
    "event_store:event_store_source",
    "running_status_logger:running_status_logger_source",
    "utility:hiview_utility",
  ]

  external_deps = [
    "hilog_native:libhilog",
    "hisysevent_native:libhisysevent",
  ]
}

ohos_shared_library("hiviewbase") {
  all_dependent_configs = [ ":hiview_base_config" ]
  public_configs = [ ":hiview_base_config" ]
  public_configs += [ "event_report:hiview_event_report_config" ]

  deps = [
    ":hiview_base",
    "$hiview_adapter/dbstore:db_store_source",
    "$hiview_adapter/utility:hiview_adapter_utility",
    "logstore:log_store",
  ]
  version_script = "libhiviewbase.map"

  part_name = "hiview"
  subsystem_name = "hiviewdfx"
  external_deps = [ "hilog_native:libhilog" ]
}

ohos_static_library("hiviewbase_static_lib_for_tdd") {
  all_dependent_configs = [ ":hiview_base_config" ]
  public_configs = [ ":hiview_base_config" ]
  public_configs += [ "event_report:hiview_event_report_config" ]

  deps = [
    ":hiview_base",
    "$hiview_adapter/dbstore:db_store_source_static_lib_for_tdd",
    "$hiview_adapter/utility:hiview_adapter_utility",
    "logstore:log_store",
  ]

  part_name = "hiview"
  subsystem_name = "hiviewdfx"
  external_deps = [ "hilog_native:libhilog" ]
}
