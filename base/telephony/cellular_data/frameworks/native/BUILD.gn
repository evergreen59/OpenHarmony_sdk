# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")

SUBSYSTEM_DIR = "//base/telephony/cellular_data"
config("cellular_data_util_config") {
  # header file path
  include_dirs = [ "$SUBSYSTEM_DIR/interfaces/innerkits" ]

  cflags = []
  if (is_double_framework) {
    cflags = [ "-DCONFIG_DUAL_FRAMEWORK" ]
  }
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  if (is_standard_system) {
    cflags += [ "-DCONFIG_STANDARD_SYSTEM" ]
  }
  if (defined(build_public_version) && build_public_version) {
    cflags += [ "-DBUILD_PUBLIC_VERSION" ]
  }
}

ohos_shared_library("tel_cellular_data_api") {
  sources = [
    "$SUBSYSTEM_DIR/frameworks/native/cellular_data_client.cpp",
    "$SUBSYSTEM_DIR/frameworks/native/cellular_data_service_proxy.cpp",
    "$SUBSYSTEM_DIR/frameworks/native/data_sim_account_call_back.cpp",
    "$SUBSYSTEM_DIR/frameworks/native/sim_account_callback_stub.cpp",
  ]

  configs = [ "//base/telephony/core_service/utils:telephony_log_config" ]

  public_configs = [ ":cellular_data_util_config" ]

  deps = [
    "//base/telephony/core_service/interfaces/innerkits:tel_core_service_api",
  ]

  external_deps = [
    "c_utils:utils",
    "hiviewdfx_hilog_native:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
  ]
  defines = [
    "TELEPHONY_LOG_TAG = \"CellularDataApi\"",
    "LOG_DOMAIN = 0xD001F03",
  ]
  part_name = "cellular_data"
  subsystem_name = "telephony"
}
