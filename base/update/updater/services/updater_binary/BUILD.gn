# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/update/updater/updater_default_cfg.gni")
import("//build/ohos.gni")

config("updater_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "//base/update/updater/interfaces/kits/include",
    "//base/update/updater/services",
    "//base/update/updater/services/include/package",
    "//base/update/updater/services/include/script",
    "//base/update/updater/services/include/log",
    "//base/update/updater/services/include",
    "//base/update/updater/utils/include",
    "//base/update/updater/utils/json",
    "//base/update/updater/services/include/applypatch",
    "//base/startup/init/interfaces/innerkits/include",
    "//third_party/openssl/include",
    "//third_party/bounds_checking_function/include",
    "//drivers/hdf/frameworks/include/utils",
  ]
}

ohos_executable("updater_binary") {
  sources = [
    "//base/update/updater/services/updater_binary/main.cpp",
    "//base/update/updater/services/updater_binary/update_image_block.cpp",
    "//base/update/updater/services/updater_binary/update_image_patch.cpp",
    "//base/update/updater/services/updater_binary/update_partitions.cpp",
    "//base/update/updater/services/updater_binary/update_processor.cpp",
  ]
  configs = [ ":updater_config" ]

  ldflags = [ "-Wl -std=c++11" ]
  if (defined(binary_force_link_libs)) {
    ldflags += [ "-Wl,--whole-archive" ]
    foreach(lib, binary_force_link_libs) {
      ldflags += [ "${lib}" ]
    }
    ldflags += [ "-Wl,--no-whole-archive" ]
  }
  deps = [
    "//base/update/updater/interfaces/kits/misc_info:libmiscinfo",
    "//base/update/updater/interfaces/kits/slot_info:libslotinfo",
    "//base/update/updater/services/applypatch:libapplypatch",
    "//base/update/updater/services/diffpatch/patch:libpatch",
    "//base/update/updater/services/fs_manager:libfsmanager",
    "//base/update/updater/services/log:libupdaterlog",
    "//base/update/updater/services/package:libupdaterpackage",
    "//base/update/updater/services/script:libupdaterscript",
    "//third_party/bzip2:libbz2",
    "//third_party/cJSON:cjson_static",
    "//third_party/lz4:liblz4_static",
    "//third_party/openssl:libcrypto_static",
    "//third_party/openssl:ssl_source",
    "//third_party/zlib:libz",
  ]

  if (defined(binary_custom_lib)) {
    deps += binary_custom_lib
  }

  if (defined(use_ptable)) {
    include_dirs = [ "//base/update/updater/services/ptable_parse" ]
    deps += [ "//base/update/updater/services/ptable_parse:libptableparse" ]
    defines = [ "UPDATER_USE_PTABLE" ]
  }

  external_deps += [ "init:libfsmanager_static" ]

  install_enable = true
  install_images = [
    "system",
    "updater",
  ]
  part_name = "updater"
  subsystem_name = "updater"
}
