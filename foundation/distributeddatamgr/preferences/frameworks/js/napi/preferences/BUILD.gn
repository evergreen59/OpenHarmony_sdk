# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")
import("//build/ohos/ace/ace.gni")
import("//foundation/distributeddatamgr/preferences/preferences.gni")
import("//foundation/distributeddatamgr/relational_store/relational_store.gni")

if (!is_mingw && !is_mac) {
  ohos_copy("preferences_declaration") {
    sources = [ "./api" ]
    outputs = [ target_out_dir + "/$target_name/" ]
    module_source_dir = target_out_dir + "/$target_name"
    module_install_name = ""
  }

  ohos_shared_library("preferences") {
    include_dirs = [
      "../common/include",
      "include",
    ]

    sources = [
      "../common/src/js_ability.cpp",
      "../common/src/js_observer.cpp",
      "../common/src/js_utils.cpp",
      "../common/src/napi_async_call.cpp",
      "../common/src/napi_preferences_observer.cpp",
      "../common/src/uv_queue.cpp",
      "src/entry_point.cpp",
      "src/napi_preferences.cpp",
      "src/napi_preferences_helper.cpp",
    ]

    deps = []

    external_deps = [
      "ability_runtime:abilitykit_native",
      "ability_runtime:napi_base_context",
      "hilog_native:libhilog",
      "napi:ace_napi",
      "preferences:native_preferences",
    ]

    subsystem_name = "distributeddatamgr"
    part_name = "preferences"
    relative_install_dir = "module/data"
  }
} else {
  ohos_shared_library("preferences") {
    include_dirs = [
      "../common/mock/include",
      "../common/include",
      "include",
      "//commonlibrary/c_utils/base/include",
    ]
    cflags_cc = [
      "-std=c++17",
      "-stdlib=libc++",
    ]
    if (is_mac) {
      buildos = "mac"
      defines = [ "MAC_PLATFORM" ]
    } else {
      buildos = "windows"
      defines = [ "WINDOWS_PLATFORM" ]
    }
    sources = [
      "../common/mock/src/js_ability.cpp",
      "../common/src/js_observer.cpp",
      "../common/src/js_utils.cpp",
      "../common/src/napi_async_call.cpp",
      "../common/src/napi_preferences_observer.cpp",
      "../common/src/uv_queue.cpp",
      "src/entry_point.cpp",
      "src/napi_preferences.cpp",
      "src/napi_preferences_helper.cpp",
    ]
    deps = [
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog_${buildos}",
      "//foundation/arkui/napi:ace_napi",
      "//foundation/distributeddatamgr/preferences/interfaces/inner_api:native_preferences",
    ]
    subsystem_name = "distributeddatamgr"
    part_name = "preferences"
    relative_install_dir = "module/data"
  }
}
