# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# http://www.apache.org/licenses/LICENSE-2.0
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/lite/config/subsystem/graphic/config.gni")
import("//build/ohos.gni")
import("//foundation/graphic/ui/ui.gni")
import("//foundation/graphic/utils/utils.gni")

config("graphic_public_config_ide") {
  include_dirs = graphic_ui_include_dirs
  include_dirs += [
    "$GRAPHIC_UI_PATH/frameworks",
    "$GRAPHIC_UI_PATH/frameworks/dock",
    "$GRAPHIC_UI_PATH/interfaces/innerkits/common",
    "$GRAPHIC_UI_PATH/interfaces/innerkits/dock",
    "$GRAPHIC_UI_PATH/interfaces/innerkits/font",
    "$GRAPHIC_UI_PATH/interfaces/kits/animator",
    "$GRAPHIC_UI_PATH/interfaces/kits/common",
    "$GRAPHIC_UI_PATH/interfaces/kits/components",
    "$GRAPHIC_UI_PATH/interfaces/kits/dfx",
    "$GRAPHIC_UI_PATH/interfaces/kits/events",
    "$GRAPHIC_UI_PATH/interfaces/kits/font",
    "$GRAPHIC_UI_PATH/interfaces/kits/layout",
    "$GRAPHIC_UI_PATH/interfaces/kits/themes",
    "$GRAPHIC_UI_PATH/interfaces/kits/window",
  ]
  defines = [
    "ENABLE_VECTOR_FONT=1",
    "ENABLE_BITMAP_FONT=1",
    "ENABLE_STATIC_FONT=1",
    "ENABLE_SHAPING=0",
    "ENABLE_ICU=1",
    "ENABLE_MULTI_FONT=1",
  ]
}

config("graphic_config_ide") {
  cflags = [
    "-Wno-deprecated-declarations",
    "-Wno-sign-compare",
    "-Wno-missing-braces",
    "-Wno-mismatched-tags",
    "-Wno-unknown-pragmas",
  ]
  cflags_cc = cflags
}

ohos_static_library("ui_ide") {
  sources = graphic_ui_sources
  include_dirs = [ "//foundation/graphic/surface/interfaces/kits" ]
  deps = [
    "//third_party/bounds_checking_function:libsec_static",
    "//third_party/cJSON:cjson_static",
    "//third_party/freetype:freetype_static",
    "//third_party/harfbuzz:harfbuzz_static",
    "//third_party/icu/icu4c/source/common:icu_font",
    "//third_party/libjpeg-turbo:turbojpeg_static",
    "//third_party/libpng:libpng_static",
    "//third_party/qrcodegen:qrcodegen_static",
  ]
  public_deps = [ ":graphic_utils_static_ide" ]
  public_configs = [ ":graphic_public_config_ide" ]
  configs = [ ":graphic_config_ide" ]
}

config("graphic_utils_public_config_ide") {
  include_dirs = graphic_utils_include_dirs
}

ohos_static_library("graphic_utils_static_ide") {
  sources = graphic_utils_sources
  include_dirs = [ "$GRAPHIC_UTILS_PATH/frameworks" ]
  public_configs = [ ":graphic_utils_public_config_ide" ]
  deps = [ "//third_party/bounds_checking_function:libsec_static" ]
  configs = [ ":graphic_config_ide" ]
}
