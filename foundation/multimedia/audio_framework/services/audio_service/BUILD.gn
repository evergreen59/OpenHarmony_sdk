# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//drivers/hdf_core/adapter/uhdf2/uhdf.gni")

pulseaudio_dir = "//third_party/pulseaudio"
pulseaudio_build_path = "//third_party/pulseaudio/ohosbuild"

group("audio_service_packages") {
  deps = [
    ":audio_service",
    ":audio_service_init",
  ]
}

config("audio_client_public_config") {
  include_dirs = [
    "//foundation/multimedia/audio_framework/frameworks/native/audiopolicy/include",
    "//foundation/multimedia/audio_framework/frameworks/native/audioutils/include",
    "//foundation/multimedia/audio_framework/frameworks/native/audiostream/include",
    "//foundation/multimedia/audio_framework/interfaces/inner_api/native/audiocapturer/include",
    "//foundation/multimedia/audio_framework/interfaces/inner_api/native/audiocommon/include",
    "//foundation/multimedia/audio_framework/interfaces/inner_api/native/audiomanager/include",
    "//foundation/multimedia/audio_framework/interfaces/inner_api/native/audiorenderer/include",
    "//foundation/multimedia/audio_framework/services/audio_service/client/include",
    "//foundation/multimedia/audio_framework/services/audio_service/common/include",
    "//foundation/multimedia/audio_framework/services/audio_policy/client/include",
    "//foundation/multimedia/audio_framework/services/audio_policy/common/include",
    "//foundation/communication/ipc/interfaces/innerkits/ipc_core/include",
    "//utils/system/safwk/native/include",
    "//commonlibrary/c_utils/base/include",
    "$pulseaudio_dir/src",
    "$pulseaudio_dir/confgure/src",
    "$pulseaudio_dir/include",
    "$pulseaudio_build_path/include",
    "//third_party/bounds_checking_function/include",
  ]

  cflags = [
    "-Wall",
    "-Werror",
  ]

  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
}

ohos_shared_library("audio_client") {
  sanitize = {
    cfi = true
    debug = false
    blocklist = "//foundation/multimedia/audio_framework/cfi_blocklist.txt"
  }
  install_enable = true
  sources = [
    "client/src/audio_device_descriptor.cpp",
    "client/src/audio_group_manager.cpp",
    "client/src/audio_manager_listener_stub.cpp",
    "client/src/audio_manager_proxy.cpp",
    "client/src/audio_routing_manager.cpp",
    "client/src/audio_stream_manager.cpp",
    "client/src/audio_stream_tracker.cpp",
    "client/src/audio_system_manager.cpp",
  ]

  public_configs = [ ":audio_client_public_config" ]

  deps = [
    "//foundation/multimedia/audio_framework/frameworks/native/audioutils:audio_utils",
    "//foundation/multimedia/audio_framework/services/audio_policy:audio_policy_client",
  ]

  if ("${product_name}" != "ohcore") {
    sources += [
         "client/src/audio_service_client.cpp",
         "client/src/audio_stream.cpp",
    ]
    deps += [
        "$pulseaudio_build_path/src/pulse:pulse",
    ]
  }

  external_deps = [
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hisysevent_native:libhisysevent",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
  ]
  subsystem_name = "multimedia"
  part_name = "multimedia_audio_framework"
}

ohos_prebuilt_etc("audio_service_init") {
  source = "etc/pulseaudio.cfg"
  relative_install_dir = "init"
  part_name = "multimedia_audio_framework"
  subsystem_name = "multimedia"
}

config("audio_service_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "common/include",
    "server/include",
    "client/include",
    "//foundation/multimedia/audio_framework/interfaces/inner_api/native/audiocommon/include",
    "//foundation/multimedia/audio_framework/interfaces/inner_api/native/audiomanager/include",
    "//foundation/multimedia/audio_framework/frameworks/native/audiopolicy/include",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/sink/common",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/sink/primary",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/sink/remote",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/sink/file",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/sink/bluetooth",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/source/common",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/source/primary",
    "//drivers/peripheral/audio/interfaces/include",
  ]

  if (target_cpu == "arm") {
    cflags = [ "-DBINDER_IPC_32BIT" ]
  }
}

ohos_shared_library("audio_service") {
  sanitize = {
    cfi = true
    debug = false
    blocklist = "//foundation/multimedia/audio_framework/cfi_blocklist.txt"
  }
  install_enable = true

  sources = [
    "server/src/audio_manager_listener_proxy.cpp",
    "server/src/audio_manager_stub.cpp",
    "server/src/audio_server.cpp",
  ]

  configs = [ ":audio_service_config" ]

  if ("${product_name}" == "m40") {
    cflags = [ "-DPRODUCT_M40" ]
  }

  deps = [
    "$pulseaudio_build_path/src/daemon:pulseaudio",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/sink:audio_renderer_sink",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/sink:bluetooth_renderer_sink",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/sink:remote_audio_renderer_sink",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/sink:renderer_sink_adapter",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/source:audio_capturer_source",
  ]

  external_deps = [
    "access_token:libaccesstoken_sdk",
    "c_utils:utils",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "power_manager:powermgr_client",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  subsystem_name = "multimedia"
  part_name = "multimedia_audio_framework"
}

group("audio_service_test_packages") {
  deps = [
    ":audio_hdi_device_test",
    ":audio_multichannel_test",
    ":audio_service_playback_test",
    ":audio_service_record_test",
  ]
}

ohos_executable("audio_hdi_device_test") {
  install_enable = false

  include_dirs = [
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/sink",
    "//foundation/multimedia/audio_framework/interfaces/inner_api/native/audiocommon/include",
    "//foundation/multimedia/audio_framework/frameworks/native/audioutils/include",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/sink/common",
    "//commonlibrary/c_utils/base/include",
    "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    "//drivers/peripheral/audio/interfaces/include",
    "//commonlibrary/c_utils/base/include",
  ]

  sources = [ "test/example/audio_hdi_device_test.cpp" ]

  deps = [
    "//foundation/multimedia/audio_framework/frameworks/native/audioutils:audio_utils",
    "//foundation/multimedia/audio_framework/frameworks/native/hdiadapter/sink:fast_audio_renderer_sink",
  ]

  external_deps = [ "hiviewdfx_hilog_native:libhilog" ]

  part_name = "multimedia_audio_framework"
  subsystem_name = "multimedia"
}

ohos_executable("audio_service_playback_test") {
  install_enable = false

  sources = [ "test/example/playback_test.cpp" ]

  configs = [ ":audio_client_public_config" ]

  deps = [ ":audio_client" ]

  external_deps = [ "hiviewdfx_hilog_native:libhilog" ]

  part_name = "multimedia_audio_framework"
  subsystem_name = "multimedia"
}

ohos_executable("audio_service_record_test") {
  install_enable = false

  sources = [ "test/example/record_test.cpp" ]

  configs = [ ":audio_client_public_config" ]

  deps = [ ":audio_client" ]

  external_deps = [ "hiviewdfx_hilog_native:libhilog" ]

  part_name = "multimedia_audio_framework"
  subsystem_name = "multimedia"
}

ohos_executable("audio_multichannel_test") {
  install_enable = false

  sources = [ "test/example/audio_multichannel_test.cpp" ]

  configs = [ ":audio_client_public_config" ]

  deps = [ ":audio_client" ]

  external_deps = [ "hiviewdfx_hilog_native:libhilog" ]

  part_name = "multimedia_audio_framework"
  subsystem_name = "multimedia"
}
