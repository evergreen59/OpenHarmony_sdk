# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/multimedia/av_session/config.gni")

group("avession_server") {
  deps = [
    ":avsession_service",
    "//foundation/multimedia/av_session/sa_profile:avsession_sa_profile",
    "//foundation/multimedia/av_session/services/etc:avsession_service.rc",
  ]
}

ohos_shared_library("avsession_service") {
  install_enable = true
  sanitize = {
    cfi = true
    debug = false
  }
  ipc_sources = [
    "ipc/proxy/avcontroller_callback_proxy.cpp",
    "ipc/proxy/avsession_callback_proxy.cpp",
    "ipc/proxy/session_listener_proxy.cpp",
    "ipc/stub/avsession_callback_stub.cpp",
    "ipc/stub/avsession_controller_stub.cpp",
    "ipc/stub/avsession_service_stub.cpp",
    "ipc/stub/avsession_stub.cpp",
  ]

  service_sources = [
    "server/avcontroller_item.cpp",
    "server/avsession_dumper.cpp",
    "server/avsession_item.cpp",
    "server/avsession_service.cpp",
    "server/background_audio_controller.cpp",
    "server/focus_session_strategy.cpp",
    "server/remote/json_utils.cpp",
    "server/remote/remote_session_command_process.cpp",
    "server/remote/remote_session_sink_proxy.cpp",
    "server/remote/remote_session_source_proxy.cpp",
    "server/session_stack.cpp",
  ]

  adapter_sources = [
    "adapter/ability_connect_helper.cpp",
    "adapter/ability_manager_adapter.cpp",
    "adapter/app_manager_adapter.cpp",
    "adapter/audio_adapter.cpp",
    "adapter/command_send_limit.cpp",
    "adapter/key_event_adapter.cpp",
    "adapter/permission_checker.cpp",
  ]

  sources = ipc_sources + adapter_sources + service_sources

  include_dirs = [
    "//foundation/multimedia/av_session/frameworks/native/session/include",
    "//foundation/multimedia/av_session/interfaces/inner_api/native/session/include",
    "//foundation/multimedia/av_session/services/session/ipc/base",
    "//foundation/multimedia/av_session/services/session/ipc/proxy",
    "//foundation/multimedia/av_session/services/session/ipc/stub",
    "//foundation/multimedia/av_session/services/session/adapter",
    "//foundation/multimedia/av_session/services/session/server",
    "//foundation/multimedia/av_session/services/session/server/remote",
    "//third_party/json/single_include",
  ]

  cflags = [
    "-fPIC",
    "-fdata-sections",
    "-ffunction-sections",
    "-fno-asynchronous-unwind-tables",
    "-fno-unwind-tables",
    "-Os",
  ]

  if (multimedia_av_session_enable_background_audio_control) {
    cflags += [ "-DENABLE_BACKGROUND_AUDIO_CONTROL" ]
  }

  if (!use_libfuzzer) {
    cflags += [ "-flto" ]
    ldflags = [
      "-flto",
      "-Wl,--gc-sections",
    ]
  }
  cflags_cc = cflags
  cflags_cc += [ "-fvisibility-inlines-hidden" ]

  defines = []
  if (target_cpu == "arm64") {
    system_lib_path = "\"/system/lib64/\""
  } else {
    system_lib_path = "\"/system/lib/\""
  }
  defines += [ "SYSTEM_LIB_PATH = ${system_lib_path}" ]

  deps = [
    "//foundation/multimedia/av_session/frameworks/common:avsession_common",
    "//foundation/multimedia/av_session/utils:avsession_utils",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:app_manager",
    "ability_runtime:wantagent_innerkits",
    "access_token:libaccesstoken_sdk",
    "bundle_framework:appexecfwk_core",
    "c_utils:utils",
    "data_object:distributeddataobject_impl",
    "device_manager:devicemanagersdk",
    "hiviewdfx_hilog_native:libhilog",
    "input:libmmi-client",
    "ipc:ipc_core",
    "multimedia_audio_framework:audio_client",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  if (multimedia_av_session_enable_trace_control) {
    cflags += [ "-DENBABLE_AVSESSION_TRACE_CONTROL" ]
    external_deps += [ "hitrace_native:hitrace_meter" ]
  }
  if (multimedia_av_session_enable_sysevent_control) {
    cflags += [ "-DENABLE_AVSESSION_SYSEVENT_CONTROL" ]
    external_deps += [ "hisysevent_native:libhisysevent" ]
  }

  subsystem_name = "multimedia"
  part_name = "multimedia_av_session"
}
