# Copyright (c) 2022-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
import("//foundation/multimedia/histreamer/config.gni")
group("plugin_standard_audio_capture") {
  deps = []
  if (hst_is_lite_sys) {
    print("std audio capture plugin is not available on lite systems")
  } else {
    deps += [ ":histreamer_plugin_StdAudioCapture" ]
  }
}

source_set("std_audio_capture") {
  sources = [
    "audio_capture_plugin.cpp",
    "audio_type_translate.cpp",
  ]
  public_deps = [
    "//foundation/multimedia/histreamer/engine/foundation:histreamer_foundation",
    "//foundation/multimedia/histreamer/engine/plugin:histreamer_plugin_intf",
  ]

  public_configs = [ "//foundation/multimedia/histreamer:histreamer_presets" ]
  if (hst_is_lite_sys) {
    print("std audio capture plugin is not available on lite systems")
  } else {
    include_dirs = [
      "//foundation/multimedia/audio_framework/interfaces/inner_api/native/audiocommon/include",
      "//commonlibrary/c_utils/base/include/",
    ]
    public_configs += [ "//foundation/multimedia/audio_framework/frameworks/native/audiocapturer:audio_capturer_config" ]
  }
}

if (hst_is_lite_sys) {
  print("std audio capture plugin is not available on lite systems")
} else {
  import("//build/ohos.gni")
  ohos_shared_library("histreamer_plugin_StdAudioCapture") {
    deps = [ ":std_audio_capture" ]
    external_deps = [ "multimedia_audio_framework:audio_capturer" ]
    relative_install_dir = "media/histreamer_plugins"
    subsystem_name = "multimedia"
    part_name = "multimedia_histreamer"
  }
}
