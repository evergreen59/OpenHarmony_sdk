# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//arkcompiler/ets_frontend/ts2panda/ts2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/ace/ace.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//foundation/multimedia/media_library/media_library.gni")

js_declaration("medialibrary_js") {
  part_name = "media_library"
  sources = [ "./@ohos.multimedia.mediaLibrary.d.ts" ]
}

ohos_copy("medialibrary_declaration") {
  sources = [ "./@ohos.multimedia.mediaLibrary.d.ts" ]
  outputs = [ target_out_dir + "/$target_name/" ]
  module_source_dir = target_out_dir + "/$target_name"
  module_install_name = ""
}

# compile .js to .abc.
action("gen_medialibraryinf_abc") {
  visibility = [ ":*" ]
  script = "//arkcompiler/ets_frontend/ts2panda/scripts/generate_js_bytecode.py"

  args = [
    "--src-js",
    rebase_path(
        "//foundation/multimedia/media_library/frameworks/js/src/medialibraryinf.js"),
    "--dst-file",
    rebase_path(target_out_dir + "/medialibraryinf.abc"),
    "--node",
    rebase_path("${node_path}"),
    "--frontend-tool-path",
    rebase_path("${ts2abc_build_path}"),
    "--node-modules",
    rebase_path("${node_modules}"),
    "--module",
  ]
  deps = [ "//arkcompiler/ets_frontend/ts2panda:ark_ts2abc_build" ]

  inputs = [ "//foundation/multimedia/media_library/frameworks/js/src/medialibraryinf.js" ]
  outputs = [ target_out_dir + "/medialibraryinf.abc" ]
}

base_output_path = get_label_info(":medialibraryinf_js", "target_out_dir")
medialibraryinf_obj_path = base_output_path + "/medialibraryinf.o"
gen_js_obj("medialibraryinf_js") {
  input = "//foundation/multimedia/media_library/frameworks/js/src/medialibraryinf.js"
  output = medialibraryinf_obj_path
  dep = ":gen_medialibraryinf_abc"
}

abc_output_path = get_label_info(":medialibraryinf_abc", "target_out_dir")
medialibraryinf_abc_obj_path = abc_output_path + "/medialibraryinf_abc.o"
gen_js_obj("medialibraryinf_abc") {
  input = "$target_out_dir/medialibraryinf.abc"
  output = medialibraryinf_abc_obj_path
  dep = ":gen_medialibraryinf_abc"
}

config("napi_config") {
  include_dirs = [
    "//drivers/peripheral/display/interfaces/include",
    "//drivers/peripheral/base",
    "//foundation/multimedia/player_framework/interfaces/inner_api/native",
    "//foundation/multimedia/media_library/frameworks/innerkitsimpl/medialibrary_data_extension/include",
    "//foundation/multimedia/media_library/frameworks/innerkitsimpl/media_library_helper/include",
    "//foundation/multimedia/media_library/frameworks/services/media_scanner/include/callback",
    "//foundation/multimedia/media_library/frameworks/services/media_thumbnail/include",
    "//foundation/multimedia/media_library/frameworks/utils/include",
    "//foundation/multimedia/media_library/interfaces/inner_api/media_library_helper/include",
    "//foundation/multimedia/media_library/interfaces/kits/js/include",
    "//foundation/multimedia/media_library/medialibrary_data_extension/include",
    "//third_party/node/src",
    "//foundation/arkui/napi/interfaces/kits",
    "//foundation/multimedia/media_library/interfaces/innerkits/native/include",
    "${ABILITY_RUNTIME_PATH}/interfaces/kits/native/ability/native",
    "${ABILITY_RUNTIME_INNER_API_PATH}/dataobs_manager/include",
    "${MEDIA_LIB_COMMON_UTILS_DIR}/include",
    "${MEDIA_LIB_SERVICES_DIR}/media_library/include",
    "//foundation/multimedia/image_framework/interfaces/innerkits/include",
    "//foundation/multimedia/image_framework/interfaces/kits/js/common/include",
    "//foundation/distributedhardware/device_manager/interfaces/inner_kits/native_cpp/include",
  ]

  ldflags = [ "-Wl" ]
}

ohos_shared_library("medialibrary") {
  configs = [ ":napi_config" ]

  sources = [
    "//foundation/multimedia/media_library/frameworks/innerkitsimpl/media_library_helper/src/media_thumbnail_helper.cpp",
    "//foundation/multimedia/media_library/frameworks/innerkitsimpl/medialibrary_data_extension/src/medialibrary_data_manager_utils.cpp",
    "//foundation/multimedia/media_library/frameworks/js/src/album_napi.cpp",
    "//foundation/multimedia/media_library/frameworks/js/src/fetch_file_result_napi.cpp",
    "//foundation/multimedia/media_library/frameworks/js/src/file_asset_napi.cpp",
    "//foundation/multimedia/media_library/frameworks/js/src/media_library_napi.cpp",
    "//foundation/multimedia/media_library/frameworks/js/src/media_scanner_napi.cpp",
    "//foundation/multimedia/media_library/frameworks/js/src/medialibrary_napi_utils.cpp",
    "//foundation/multimedia/media_library/frameworks/js/src/napi_error.cpp",
    "//foundation/multimedia/media_library/frameworks/js/src/native_module_ohos_medialibrary.cpp",
    "//foundation/multimedia/media_library/frameworks/js/src/smart_album_napi.cpp",
    "//foundation/multimedia/media_library/frameworks/js/src/userfile_client.cpp",
  ]

  deps = [
    ":medialibraryinf_abc",
    ":medialibraryinf_js",
    "${ABILITY_RUNTIME_INNER_API_PATH}/dataobs_manager:dataobs_manager",
    "${ABILITY_RUNTIME_PATH}/frameworks/native/ability/native:abilitykit_native",
    "//foundation/multimedia/media_library/frameworks/innerkitsimpl/media_library_helper:media_library",
  ]

  external_deps = [
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:ability_manager",
    "ability_runtime:napi_base_context",
    "c_utils:utils",
    "data_share:datashare_common",
    "data_share:datashare_consumer",
    "hitrace_native:hitrace_meter",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "ipc_js:rpc",
    "multimedia_image_framework:image",
    "multimedia_player_framework:media_client",
    "napi:ace_napi",
    "relational_store:native_rdb",
  ]

  sanitize = {
    cfi = true
    debug = false
  }

  relative_install_dir = "module/multimedia"
  subsystem_name = "multimedia"
  part_name = "media_library"
}

ohos_shared_library("userfilemanager") {
  configs = [ ":napi_config" ]

  sources = [ "//foundation/multimedia/media_library/frameworks/js/src/native_module_ohos_userfile_manager.cpp" ]

  deps = [
    ":medialibrary",
    "${ABILITY_RUNTIME_PATH}/frameworks/native/ability/native:abilitykit_native",
  ]

  external_deps = [
    "ability_runtime:napi_base_context",
    "c_utils:utils",
    "data_share:datashare_consumer",
    "hitrace_native:hitrace_meter",
    "ipc_js:rpc",
    "multimedia_image_framework:image",
    "multimedia_player_framework:media_client",
    "napi:ace_napi",
    "relational_store:native_dataability",
    "relational_store:native_rdb",
  ]

  sanitize = {
    cfi = true
    debug = false
  }

  relative_install_dir = "module/filemanagement"
  subsystem_name = "multimedia"
  part_name = "media_library"
}
