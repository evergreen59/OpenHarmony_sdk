# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/ets_frontend/ts2panda/ts2abc_config.gni")
import("//build/ohos.gni")

source_root = "//test/testfwk/arkxtest/uitest"

config("uitest_common_configs") {
  include_dirs = [
    "//third_party/json/single_include/nlohmann",
    "//foundation/mutimodalinput/input/interfaces",
  ]
  cflags = [
    "-Wfloat-equal",
    "-Wformat=2",
    "-Wshadow",
  ]
}

ohos_static_library("uitest_core") {
  use_exceptions = true
  configs = [ ":uitest_common_configs" ]
  sources = [
    "${source_root}/core/frontend_api_handler.cpp",
    "${source_root}/core/rect_algorithm.cpp",
    "${source_root}/core/ui_action.cpp",
    "${source_root}/core/ui_controller.cpp",
    "${source_root}/core/ui_driver.cpp",
    "${source_root}/core/ui_model.cpp",
    "${source_root}/core/widget_matcher.cpp",
    "${source_root}/core/widget_operator.cpp",
    "${source_root}/core/widget_selector.cpp",
    "${source_root}/core/window_operator.cpp",
  ]
  external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
  defines = [
    "__OHOS__=1",
    "LOG_TAG=\"UiTestKit_Base\"",
  ]
}

ohos_static_library("uitest_record") {
  use_exceptions = true
  configs = [ ":uitest_common_configs" ]
  sources = [
    "${source_root}/record/find_widget.cpp",
    "${source_root}/record/least_square_impl.cpp",
    "${source_root}/record/matrix3.cpp",
    "${source_root}/record/matrix4.cpp",
    "${source_root}/record/ui_record.cpp",
    "${source_root}/record/velocity_tracker.cpp",
  ]
  deps = [ ":uitest_core" ]
  defines = [
    "__OHOS__=1",
    "LOG_TAG=\"UiTestKit_Base\"",
  ]
  include_dirs = [ "${source_root}/core" ]
  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_manager",
    "hiviewdfx_hilog_native:libhilog",
    "input:libmmi-client",
  ]
}
common_external_deps = [
  "c_utils:utils",
  "hiviewdfx_hilog_native:libhilog",
]

ohos_static_library("uitest_ipc") {
  configs = [ ":uitest_common_configs" ]
  sources = [ "${source_root}/connection/ipc_transactor.cpp" ]
  include_dirs = [ "${source_root}/core" ]
  external_deps = [
    "ability_base:want",
    "common_event_service:cesfwk_innerkits",
    "ipc:ipc_core",
  ]
  external_deps += common_external_deps

  defines = [
    "__OHOS__=1",
    "LOG_TAG=\"UiTestKit_Conn\"",
  ]
}

ohos_executable("uitest_server") {
  configs = [ ":uitest_common_configs" ]
  sources = [
    "${source_root}/server/server_main.cpp",
    "${source_root}/server/system_ui_controller.cpp",
  ]
  include_dirs = [
    "${source_root}/core",
    "${source_root}/connection",
    "${source_root}/record",
  ]

  # static link the uitest_core as server end
  deps = [
    ":uitest_core",
    ":uitest_ipc",
    ":uitest_record",
  ]

  #dependencies for screencap
  deps += [
    "//foundation/graphic/graphic_2d/rosen/modules/render_service_client:librender_service_client",
    "//third_party/libpng:libpng",
  ]
  external_deps = [
    "accessibility:accessibleability",
    "input:libmmi-client",
    "multimedia_image_framework:image_native",
    "window_manager:libdm",
    "window_manager:libwm",
  ]

  subsystem_name = "test"
  part_name = "arkXtest"
  output_name = "uitest"
  install_enable = true
  external_deps += common_external_deps
  defines = [
    "__OHOS__=1",
    "LOG_TAG=\"UiTestKit_Server\"",
  ]
}

ts2abc_gen_abc("gen_uitest_exporter_abc") {
  src_js = rebase_path("${source_root}/napi/uitest_exporter.js")
  dst_file = rebase_path(target_out_dir + "/uitest_exporter.abc")
  in_puts = [ "${source_root}/napi/uitest_exporter.js" ]
  out_puts = [ target_out_dir + "/uitest_exporter.abc" ]
  extra_args = [ "--module" ]
}

gen_js_obj("uitest_exporter_js") {
  input = "${source_root}/napi/uitest_exporter.js"
  output = target_out_dir + "/uitest_exporter.o"
}

gen_js_obj("uitest_exporter_abc") {
  input = get_label_info(":gen_exporter_abc", "target_out_dir") +
          "/uitest_exporter.abc"
  output = target_out_dir + "/uitest_exporter_abc.o"
  dep = ":gen_uitest_exporter_abc"
}

ohos_shared_library("uitest_client") {
  configs = [ ":uitest_common_configs" ]
  sources = [ "${source_root}/napi/uitest_napi.cpp" ]
  include_dirs = [
    "${source_root}/core",
    "${source_root}/connection",
  ]
  deps = [
    ":uitest_exporter_abc",
    ":uitest_exporter_js",
    ":uitest_ipc",
  ]
  subsystem_name = "test"
  part_name = "arkXtest"
  output_name = "uitest"
  external_deps = common_external_deps
  external_deps += [ "napi:ace_napi" ]
  relative_install_dir = "module"
  defines = [
    "__OHOS__=1",
    "LOG_TAG=\"UiTestKit_Client\"",
  ]
}

ohos_unittest("uitest_core_unittest") {
  sources = [
    "${source_root}/test/common_utilities_test.cpp",
    "${source_root}/test/frontend_api_handler_test.cpp",
    "${source_root}/test/rect_algorithm_test.cpp",
    "${source_root}/test/ui_action_test.cpp",
    "${source_root}/test/ui_controller_test.cpp",
    "${source_root}/test/ui_driver_test.cpp",
    "${source_root}/test/ui_model_test.cpp",
    "${source_root}/test/widget_matcher_test.cpp",
    "${source_root}/test/widget_operator_test.cpp",
    "${source_root}/test/widget_selector_test.cpp",
  ]
  deps = [
    ":uitest_core",
    "//third_party/googletest:gtest_main",
  ]
  include_dirs = [
    "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    "//third_party/json/single_include/nlohmann",
    "${source_root}/core",
  ]
  cflags = [ "-g" ]
  cflags_cc = [ "-g" ]
  use_exceptions = true
  module_out_path = "uitestkit/unittest"
  testonly = true
  subsystem_name = "test"
  part_name = "arkXtest"
}

ohos_unittest("uitest_ipc_unittest") {
  sources = [ "${source_root}/test/ipc_transactor_test.cpp" ]
  deps = [
    ":uitest_ipc",
    "//third_party/googletest:gtest_main",
  ]
  include_dirs = [
    "${source_root}/connection",
    "${source_root}/core",
    "//third_party/json/single_include/nlohmann",
  ]
  cflags = [ "-g" ]
  cflags_cc = [ "-g" ]
  module_out_path = "uitestkit/unittest"
  testonly = true
  subsystem_name = "test"
  part_name = "arkXtest"
}

group("uitestkit") {
  deps = [
    ":uitest_client",
    ":uitest_server",
  ]
}

group("uitestkit_test") {
  testonly = true
  deps = [
    ":uitest_core_unittest",
    ":uitest_ipc_unittest",
  ]
}
