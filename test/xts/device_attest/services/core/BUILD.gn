# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//test/xts/device_attest/build/devattestconfig.gni")

sources_common = [
  "attest_entry.c",
  "adapter/attest_adapter.c",
  "adapter/attest_adapter_os.c",
  "adapter/attest_adapter_oem.c",
  "adapter/attest_adapter_mock.c",
  "attest/attest_service.c",
  "attest/attest_service_auth.c",
  "attest/attest_service_active.c",
  "attest/attest_service_challenge.c",
  "attest/attest_service_reset.c",
  "attest/attest_service_device.c",
  "network/attest_network.c",
  "security/attest_security.c",
  "security/attest_security_token.c",
  "security/attest_security_ticket.c",
  "utils/attest_utils_file.c",
  "utils/attest_utils_json.c",
  "utils/attest_utils_log.c",
  "utils/attest_utils_timer.c",
  "utils/attest_utils_list.c",
  "utils/attest_utils.c",
]

if (enable_attest_debug_memory_leak) {
  sources_common += [ "utils/attest_utils_memleak.c" ]
}

if (enable_attest_debug_dfx) {
  sources_common += [ "dfx/attest_dfx.c" ]
}

config("devattest_core_config") {
  visibility = [ ":*" ]
  cflags = [ "-Wall" ]
  include_dirs = [
    "include",
    "${devattest_path}/services/core/include/adapter",
    "${devattest_path}/services/core/include/attest",
    "${devattest_path}/services/core/include/dfx",
    "${devattest_path}/services/core/include/security",
    "${devattest_path}/services/core/include/network",
    "${devattest_path}/services/core/include/utils",
  ]

  defines = [ "ATTEST_HILOG_LEVEL = 1" ]

  if (enable_attest_test_mock_network) {
    defines += [ "__ATTEST_MOCK_NETWORK_STUB__" ]
  }
  if (enable_attest_test_mock_device) {
    defines += [ "__ATTEST_MOCK_DEVICE_STUB__" ]
  }
  if (enable_attest_debug_memory_leak) {
    defines += [ "__ATTEST_DEBUG_MEMORY_LEAK__" ]
  }
  if (enable_attest_network_debug_log) {
    defines += [ "__ATTEST_NETWORK_DEBUG_LOG__" ]
  }

  defines += [ "MBEDTLS_ALLOW_PRIVATE_ACCESS" ]
}

ohos_prebuilt_etc("network_config.json") {
  source = "network_config.json"
  relative_install_dir = "device_attest"
  subsystem_name = "xts"
  part_name = "device_attest"
}

ohos_shared_library("devattest_core") {
  sources = sources_common

  configs = [ ":devattest_core_config" ]

  deps = [
    "${devattest_path}/services/oem_adapter:device_attest_oem_adapter",
    "//third_party/cJSON:cjson",
    "//third_party/mbedtls:mbedtls_shared",
    "//third_party/openssl:libcrypto_shared",
    "//third_party/openssl:libssl_shared",
  ]

  deps += [ ":network_config.json" ]

  external_deps = [
    "c_utils:utils",
    "hiviewdfx_hilog_native:libhilog",
    "init:libbegetutil",
    "syscap_codec:syscap_interface_shared",
  ]

  subsystem_name = "xts"
  part_name = "device_attest"
}
